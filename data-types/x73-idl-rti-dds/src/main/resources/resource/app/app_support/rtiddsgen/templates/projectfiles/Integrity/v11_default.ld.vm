##
##
## (c) Copyright, Real-Time Innovations, Inc. 2001.  All rights reserved.
## No duplications, whole or partial, manual or electronic, may be made
## without prior written permission.  Any such copies, or
## revisions thereof, must display this notice unaltered.
## This code contains trade secrets of Real-Time Innovations, Inc.
##
## modification history:
## ---------------------
## 2.2,9jun14,vmg created
##========================================================================
// --- Default link map for INTEGRITY Virtual Address Spaces ---
//
// Section notes are interleaved within the link map.  More information
// can be found in the "Linker Directives Files" section of the "Building
// INTEGRITY Applications" chapter of the INTEGRITY Development Guide.
//

// These default values may be overridden by using -C options on the linker
// command line. Do not change these default values directly.
// Note also that some .bld files in the target directory already override
// these defaults.
DEFAULTS {
    __INTEGRITY_UserLoadBaseAddress   = 64K
    __INTEGRITY_MinPageAlign          = 16K
    __INTEGRITY_MaxPageAlign          = 16K
}

// text (readonly) and data (read/write) areas must be page-aligned
SECTIONS {
// text/readonly segment
    .picbase        __INTEGRITY_UserLoadBaseAddress :
    .text                                           :
    .syscall                                        :
    .intercall                             ALIGN(4) :
    .interfunc                                      :
    .secinfo                                        :
    .rodata                               ALIGN(16) :
    .sdata2                               ALIGN(16) :
    .fixaddr                                        :
    .fixtype                                        :
// .textchecksum MUST be the last section of the text/readonly segment
    .textchecksum                                   :

// data/readwrite segment
    .pidbase        ALIGN(__INTEGRITY_MaxPageAlign) :
    .data                                           :
    .sdabase                               ALIGN(8) :
    .sdata                                 ALIGN(8) :
    .toc                                            :
    .opd                                            :
// .datachecksum MUST be the last section of the data portion of the 
// data/read/write segment (i.e., it must come before the BSS sections).
    .datachecksum                                   :

// No intitialized data sections should be placed after these bss sections
// unless the first such data section has been aligned to a page boundary.
// Failure to do so will cause all of bss to be pulled into the previous data
// segment, thereby increasing the size of the dynamic download image that
// needs to be transferred to the target.
    .sbss                                           :
    .bss                                            :
    .earlybss                               NOCLEAR :
    .tksa   ALIGN(__INTEGRITY_MinPageAlign) NOCLEAR :
        
// intex will create a heap section unless the following section is uncommented.
// The size of intex heap section defaults to the value of DefaultHeapSize in
// default.bsp.  To override for a particular AddressSpace, define HeapSize in
// the AddressSpace section in the .int file.
//  .heap                    ALIGN(16) PAD(0x10000) :


    
// Define symbols for members of __ghs_LoaderLoadeeInitialSharedData written by
// intex.
    __ghs_intbegin_heap = __ghs_LoaderLoadeeInitialSharedData +
    16 + 0 * ADDRESS_SIZE;
    __ghs_intend_heap = __ghs_LoaderLoadeeInitialSharedData +
    16 + 1 * ADDRESS_SIZE;
}
